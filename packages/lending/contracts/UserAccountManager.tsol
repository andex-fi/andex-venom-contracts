pragma ever-solidity >= 0.62.0;
pragma AbiHeader pubkey;
pragma AbiHeader expire;
pragma AbiHeader time;

import './interfaces/IUserAccountManager.tsol';
import './interfaces/IUAMUserAccount.tsol';
import './interfaces/IUAMMarket.tsol';
import "./interfaces/IUpgradeableContract.tsol";
import './interfaces/IMarket.tsol';
import './interfaces/IModule.tsol';

import "./libraries/ErrorCodes.tsol";
import './libraries/CostConstants.tsol';
import "./libraries/MsgFlag.tsol";
import "./libraries/OperationCodes.tsol";

import './abstract/Roles.tsol';

import './UserAccount.tsol';

contract UserAccountManager is Roles, IUpgradeableContract, IUserAccountManager, IUAMUserAccount, IUAMMarket {
    // Information for update
    uint32 public contractCodeVersion;

    address public marketAddress;
    mapping(uint8 => address) public modules;
    mapping(address => bool) public existingModules;
    mapping(uint32 => TvmCell) public userAccountCodes;

    event AccountCreated(address venomWallet, address userAddress);

    /*********************************************************************************************************/
    // Functions for deployment and upgrade
    // Contract is deployed via platform
    constructor(address _newOwner) public {
        tvm.accept();
        _owner = _newOwner;
    }

    /*  Upgrade Data for version 1 (from version 0):
        bits:
            address root
            uint8 contractType
            uint32 codeVersion
        refs:
            1. platformCode
            2. additionalData:
            bits:
                1. address marketAddress
            refs:
                1. mapping(uint32 => bool) marketIds
     */
    function upgradeContractCode(
        TvmCell code, 
        TvmCell updateParams, 
        uint32 codeVersion
    ) override external canUpgrade {
        tvm.accept();

        tvm.setcode(code);
        tvm.setCurrentCode(code);

        onCodeUpgrade(
            _owner,
            marketAddress,
            modules,
            existingModules,
            userAccountCodes,
            updateParams,
            codeVersion
        );
    }

    function onCodeUpgrade(
        address owner,
        address _marketAddress,
        mapping(uint8 => address) _modules,
        mapping(address => bool) _existingModules,
        mapping(uint32 => TvmCell) _userAccountCodes,
        TvmCell,
        uint32 _codeVersion
    ) private {
        tvm.accept();
        tvm.resetStorage();
        contractCodeVersion = _codeVersion;
        _owner = owner;
        marketAddress = _marketAddress;
        modules = _modules;
        existingModules = _existingModules;
        userAccountCodes = _userAccountCodes;
    }

        /*********************************************************************************************************/
    // Functions for user account
    /**
     * @param venomWallet Address of user's venom wallet
     */
    function createUserAccount(address venomWallet) external override view {
        TvmSlice ts = userAccountCodes[0].toSlice();
        require(!ts.empty());

        address userAccount = new UserAccount{
            value: CostConstants.USE_FOR_UA_DEPLOY,
            code: userAccountCodes[0],
            pubkey: 0,
            varInit: {
                owner: venomWallet
            }
        }();

        emit AccountCreated(venomWallet, userAccount);

        _updateUserAccount(venomWallet);
    }

    // address calculation functions
    /**
     * @param venomWallet Address of user's venom wallet
     */
    function calculateUserAccountAddress(address venomWallet) external override responsible view returns (address) {
        return { value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED } _calculateUserAccountAddress(venomWallet);
    }

    /**
     * @param venomWallet Address of user's venom wallet
     */
    function _calculateUserAccountAddress(address venomWallet) internal view returns(address) {
        return address(tvm.hash(_buildUserAccountData(venomWallet)));
    }

    /**
     * @param venomWallet Address of user's venom wallet
     */
    function _buildUserAccountData(address venomWallet) private view returns (TvmCell data) {
        return tvm.buildStateInit({
            contr: UserAccount,
            varInit: {
                owner: venomWallet
            },
            pubkey: 0,
            code: userAccountCodes[0]
        });
    }

    /*********************************************************************************************************/
    // Supply operations

    function writeSupplyInfo(
        address venomWallet,
        uint32 marketId, 
        uint256 tokensToSupply, 
        fraction index
    ) external override view onlyModule(OperationCodes.SUPPLY_TOKENS) {
        address userAccount = _calculateUserAccountAddress(venomWallet);
        IUserAccount(userAccount).writeSupplyInfo{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(marketId, tokensToSupply, index);
    }

    /*********************************************************************************************************/
    // Withdraw operations

    function requestWithdraw(
        address venomWallet, 
        address userTip3Wallet, 
        uint32 marketId, 
        uint256 tokensToWithdraw
    ) external override view onlyValidUserAccount(venomWallet) {
        TvmBuilder tb;
        tb.store(venomWallet);
        tb.store(userTip3Wallet);
        tb.store(tokensToWithdraw);
        IMarket(marketAddress).performOperationUserAccountManager{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(OperationCodes.WITHDRAW_TOKENS, marketId, tb.toCell());
    }

    function requestWithdrawInfo(
        address venomWallet, 
        address userTip3Wallet,
        uint256 tokensToWithdraw, 
        uint32 marketId, 
        mapping(uint32 => fraction) updatedIndexes
    ) external override view onlyModule(OperationCodes.WITHDRAW_TOKENS) {
        address userAccount = _calculateUserAccountAddress(venomWallet);
        IUserAccount(userAccount).requestWithdrawInfo{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(userTip3Wallet, marketId, tokensToWithdraw, updatedIndexes);
    }

    function receiveWithdrawInfo(
        address venomWallet, 
        address userTip3Wallet,
        uint256 tokensToWithdraw,
        uint32 marketId,
        mapping(uint32 => uint256) supplyInfo,
        mapping(uint32 => BorrowInfo) borrowInfo
    ) external override view onlyValidUserAccount(venomWallet) {
        IModule(modules[OperationCodes.WITHDRAW_TOKENS]).withdrawTokensFromMarket{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(venomWallet, userTip3Wallet, tokensToWithdraw, marketId, supplyInfo, borrowInfo);
    }

    function writeWithdrawInfo(
        address venomWallet, 
        address userTip3Wallet, 
        uint32 marketId, 
        uint256 tokensToWithdraw, 
        uint256 tokensToSend
    ) external override view onlyModule(OperationCodes.WITHDRAW_TOKENS) {
        address userAccount = _calculateUserAccountAddress(venomWallet); 
        IUserAccount(userAccount).writeWithdrawInfo{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(userTip3Wallet, marketId, tokensToWithdraw, tokensToSend);
    }

    /*********************************************************************************************************/
    // Borrow operations

    function requestIndexUpdate(
        address venomWallet, 
        uint32 marketId, 
        TvmCell args
    ) external override view onlyValidUserAccount(venomWallet) {
        IMarket(marketAddress).performOperationUserAccountManager{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(OperationCodes.BORROW_TOKENS, marketId, args);
    }

    function updateUserIndexes(
        address venomWallet, 
        address userTip3Wallet, 
        uint256 tokensToBorrow, 
        uint32 marketId,
        mapping(uint32 => fraction) updatedIndexes
    ) external override view onlyModule(OperationCodes.BORROW_TOKENS) {
        address userAccount = _calculateUserAccountAddress(venomWallet);
        IUserAccount(userAccount).borrowUpdateIndexes{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(marketId, updatedIndexes, userTip3Wallet, tokensToBorrow);
    }

    function passBorrowInformation(
        address venomWallet, 
        address userTip3Wallet, 
        uint32 marketId, 
        uint256 tokensToBorrow, 
        mapping(uint32 => uint256) supplyInfo, 
        mapping(uint32 => BorrowInfo) borrowInfo
    ) external override view onlyValidUserAccount(venomWallet) {
        IModule(modules[OperationCodes.BORROW_TOKENS]).borrowTokensFromMarket{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(venomWallet, userTip3Wallet, tokensToBorrow, marketId, supplyInfo, borrowInfo);
    }

    function writeBorrowInformation(
        address venomWallet, 
        address userTip3Wallet, 
        uint256 tokensToBorrow, 
        uint32 marketId, 
        fraction index
    ) external override view onlyModule(OperationCodes.BORROW_TOKENS) {
        address userAccount = _calculateUserAccountAddress(venomWallet);
        IUserAccount(userAccount).writeBorrowInformation{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(marketId, tokensToBorrow, userTip3Wallet, index);
    }

    /*********************************************************************************************************/
    // Repay operations

    function requestRepayInfo(
        address venomWallet, 
        address userTip3Wallet, 
        uint256 tokensForRepay, 
        uint32 marketId,
        mapping(uint32 => fraction) updatedIndexes
    ) external override view onlyModule(OperationCodes.REPAY_TOKENS) {
        address userAccount = _calculateUserAccountAddress(venomWallet);
        IUserAccount(userAccount).sendRepayInfo{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(userTip3Wallet, marketId, tokensForRepay, updatedIndexes);
    }

    function receiveRepayInfo(
        address venomWallet, 
        address userTip3Wallet, 
        uint256 tokensForRepay,
        uint32 marketId,
        BorrowInfo borrowInfo
    ) external override view onlyValidUserAccount(venomWallet) {
        IModule(modules[OperationCodes.REPAY_TOKENS]).repayLoan{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(venomWallet, userTip3Wallet, tokensForRepay, marketId, borrowInfo);
    }

    function writeRepayInformation(
        address venomWallet, 
        address userTip3Wallet, 
        uint32 marketId,
        uint256 tokensToReturn, 
        BorrowInfo bi
    ) external override view onlyModule(OperationCodes.REPAY_TOKENS) {
        address userAccount = _calculateUserAccountAddress(venomWallet);
        IUserAccount(userAccount).writeRepayInformation{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(userTip3Wallet, marketId, tokensToReturn, bi);
    }

    /*********************************************************************************************************/
    // Liquidation

    function requestLiquidationInformation(
        address venomWallet, 
        address targetUser, 
        address tip3UserWallet, 
        uint32 marketId, 
        uint32 marketToLiquidate,
        uint256 tokensProvided,
        mapping(uint32 => fraction) updatedIndexes
    ) external override view onlyModule(OperationCodes.LIQUIDATE_TOKENS) {
        address userAccount = _calculateUserAccountAddress(targetUser);
        IUserAccount(userAccount).requestLiquidationInformation{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(venomWallet, tip3UserWallet, marketId, marketToLiquidate, tokensProvided, updatedIndexes);
    }

    function receiveLiquidationInformation(
        address venomWallet, 
        address targetUser, 
        address tip3UserWallet, 
        uint32 marketId, 
        uint32 marketToLiquidate,
        uint256 tokensProvided, 
        mapping(uint32 => uint256) supplyInfo, 
        mapping(uint32 => BorrowInfo) borrowInfo
    ) external override view onlyValidUserAccount(targetUser) {
        IModule(modules[OperationCodes.LIQUIDATE_TOKENS]).liquidate{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(venomWallet, targetUser, tip3UserWallet, marketId, marketToLiquidate, tokensProvided, supplyInfo, borrowInfo);
    }

    function seizeTokens(
        address venomWallet,
        address targetUser,
        address tip3UserWallet,
        uint32 marketId,
        uint32 marketToLiquidate,
        uint256 tokensToSeize, 
        uint256 tokensToReturn,
        BorrowInfo borrowInfo
    ) external override view onlyModule(OperationCodes.LIQUIDATE_TOKENS) {
        address userAccount = _calculateUserAccountAddress(targetUser);
        IUserAccount(userAccount).liquidateVTokens{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(venomWallet, tip3UserWallet, marketId, marketToLiquidate, tokensToSeize, tokensToReturn, borrowInfo);
    }

    function grantVTokens(
        address venomWallet, 
        address targetUser,
        address tip3UserWallet,
        uint32 marketId, 
        uint32 marketToLiquidate,
        uint256 vTokensToGrant, 
        uint256 tokensToReturn
    ) external override view onlyValidUserAccountNoReserve(targetUser) {
        
        address targetAccount = _calculateUserAccountAddress(targetUser);
        IUserAccount(targetAccount).checkUserAccountHealth{
            value: CostConstants.UPDATE_HEALTH_COST
        }(venomWallet);

        address userAccount = _calculateUserAccountAddress(venomWallet);
        IUserAccount(userAccount).grantVTokens{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(targetUser, tip3UserWallet, marketId, marketToLiquidate, vTokensToGrant, tokensToReturn);
    }

    function abortLiquidation(
        address venomWallet,
        address targetUser,
        address tip3UserWallet, 
        uint32 marketId, 
        uint256 tokensToReturn
    ) external override view onlyModule(OperationCodes.LIQUIDATE_TOKENS) {
        address userAccount = _calculateUserAccountAddress(targetUser);
        IUserAccount(userAccount).abortLiquidation{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(venomWallet, tip3UserWallet, marketId, tokensToReturn);
    }

    function returnAndSupply(
        address venomWallet,
        address tip3UserWallet,
        address userToUnlock,
        uint32 marketId,
        uint256 tokensToReturn
    ) external override view onlyValidUserAccountNoReserve(venomWallet) {
        if (tokensToReturn != 0) {
            uint128 venomsToUse = msg.value / 4;

            IMarket(marketAddress).requestTokenPayout{
                value: venomsToUse * 3
            }(venomWallet, tip3UserWallet, marketId, tokensToReturn);
        }

        TvmBuilder tb;
        tb.store(venomWallet);
        ILockable(modules[OperationCodes.LIQUIDATE_TOKENS]).unlock{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(userToUnlock, tb.toCell());
    }

    /*********************************************************************************************************/
    // Convert vTokens to TIP-3 tokens

    function requestVTokensConversion(
        address owner, 
        uint256 amount, 
        uint32 marketId
    ) external override view onlyValidUserAccountNoReserve(owner) {
        TvmBuilder tb;
        tb.store(owner);
        tb.store(amount);
        tb.store(marketId);
        IMarket(marketAddress).performOperationUserAccountManager{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(OperationCodes.CONVERT_VTOKENS, marketId, tb.toCell());
    }

    function requestConversionInfo(
        address _user, 
        uint256 _amount, 
        uint32 _marketId
    ) external override view onlyModule(OperationCodes.CONVERT_VTOKENS) {
        address userAccount = _calculateUserAccountAddress(_user); 
        IUserAccount(userAccount).requestConversionInfo{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(_amount, _marketId);
    }

    function receiveConversionInfo(
        address _user, 
        uint256 _amount, 
        uint32 marketId, 
        mapping (uint32 => uint256) supplyInfo,
        mapping (uint32 => BorrowInfo) borrowInfo
    ) external override view onlyValidUserAccountNoReserve(_user) {
        IModule(modules[OperationCodes.CONVERT_VTOKENS]).performConversion{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(_user, _amount, marketId, supplyInfo, borrowInfo);
    }

    function writeConversionInfo(
        address _user, 
        uint256 _amount, 
        bool positive, 
        uint32 marketId
    ) external override view onlyValidUserAccountNoReserve(_user) {
        address userAccount = _calculateUserAccountAddress(_user); 
        IUserAccount(userAccount).writeConversionInfo{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(_amount, positive, marketId);
    }

    function unlockAfterConversion(
        address _user
    ) external override view onlyValidUserAccountNoReserve(_user) {
        TvmCell empty;
        ILockable(modules[OperationCodes.CONVERT_VTOKENS]).unlock{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(_user, empty);
    }

    /*********************************************************************************************************/
    // Account health calculation

    function requestUserAccountHealthCalculation(address venomWallet) external override view executor {
        address userAccount = _calculateUserAccountAddress(venomWallet);
        IUserAccount(userAccount).checkUserAccountHealth{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(venomWallet);
    }

    function calculateUserAccountHealth(
        address venomWallet, 
        address gasTo,
        mapping(uint32 => uint256) supplyInfo,
        mapping(uint32 => BorrowInfo) borrowInfo,
        TvmCell dataToTransfer
    ) external override view onlyValidUserAccount(venomWallet) {
        IMarket(marketAddress).calculateUserAccountHealth{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(venomWallet, gasTo, supplyInfo, borrowInfo, dataToTransfer);
    }

    function updateUserAccountHealth(
        address venomWallet, 
        address gasTo,
        fraction accountHealth, 
        mapping(uint32 => fraction) updatedIndexes,
        TvmCell dataToTransfer
    ) external override view onlyMarket {
        address userAccount = _calculateUserAccountAddress(venomWallet);
        IUserAccount(userAccount).updateUserAccountHealth{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(gasTo, accountHealth, updatedIndexes, dataToTransfer);
    }

    /*********************************************************************************************************/
    // Requests from user account to market

    function requestTokenPayout(address venomWallet, address userTip3Wallet, uint32 marketId, uint256 toPayout) external override view onlySelectedExecutors(OperationCodes.LIQUIDATE_TOKENS, venomWallet) {
        IMarket(marketAddress).requestTokenPayout{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(venomWallet, userTip3Wallet, marketId, toPayout);
    }

    function withdrawExtraTons(address venomWallet) external view onlyOwner {
        tvm.accept();
        address(venomWallet).transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED});
    }

    /*********************************************************************************************************/
    // Market managing functions

    /**
     * @param _market Address of market smart contract
     */
    function setMarketAddress(address _market) external override canChangeParams {
        tvm.accept();
        marketAddress = _market;
        address(_owner).transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED});
    }

    /*********************************************************************************************************/
    // Function for userAccountCode
    function uploadUserAccountCode(uint32 version, TvmCell code) external override canChangeParams {
        userAccountCodes[version] = code;
        
        address(msg.sender).transfer({flag: MsgFlag.ALL_NOT_RESERVED, value: 0});
    }

    function _updateUserAccount(address venomWallet) internal view {
        address userAccount = _calculateUserAccountAddress(venomWallet);
        optional(uint32, TvmCell) latestVersion = userAccountCodes.max();
        if (latestVersion.hasValue()) {
            TvmCell empty;
            (uint32 codeVersion, TvmCell code) = latestVersion.get();
            IUpgradeableContract(userAccount).upgradeContractCode{
                flag: MsgFlag.ALL_NOT_RESERVED
            }(code, empty, codeVersion);
        } else {
            address(userAccount).transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED});
        }
    }

    function updateUserAccount(address venomWallet) external override {
        _updateUserAccount(venomWallet);
    }

    function getUserAccountCode(uint32 version) external override view responsible returns(TvmCell) {
        return {
            value: 0,
            bounce: false,
            flag: MsgFlag.ALL_NOT_RESERVED
        } userAccountCodes[version];
    }

    function disableUserAccountLock(address venomWallet) external view onlyOwner {
        address userAccount = _calculateUserAccountAddress(venomWallet);
        IUserAccount(userAccount).disableBorrowLock{
            flag: MsgFlag.ALL_NOT_RESERVED
        }();
    }

    function removeMarket(address venomWallet, uint32 marketId) external view canChangeParams {
        address userAccount = _calculateUserAccountAddress(venomWallet);
        IUserAccount(userAccount).removeMarket{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(marketId);
    }

    /*********************************************************************************************************/
    // Functions to add/remove modules info
    function addModule(uint8 operationId, address module) external override onlyTrusted {
        delete existingModules[module];
        modules[operationId] = module;
        existingModules[module] = true;
    }

    function removeModule(uint8 operationId) external override onlyTrusted {
        delete existingModules[modules[operationId]];
        delete modules[operationId];
    }

    /*********************************************************************************************************/
    // modifiers

    modifier onlyMarket() {
        require(
            msg.sender == marketAddress,
            ErrorCodes.ERROR_NOT_MARKET
        );
        _;
    }

    modifier onlyTrusted() {
        require(
            msg.sender == _owner ||
            msg.sender == marketAddress ||
            _canChangeParams[msg.sender],
            ErrorCodes.ERROR_NOT_TRUSTED
        );
        _;
    }

    modifier onlyModules() {
        require(
            existingModules.exists(msg.sender),
            ErrorCodes.ERROR_NOT_MODULE
        );
        _;
    }

    modifier executor() {
        require(
            msg.sender == _owner ||
            msg.sender == marketAddress ||
            existingModules.exists(msg.sender),
            ErrorCodes.ERROR_NOT_EXECUTOR
        );
        _;
    }

    modifier onlyModule(uint8 operationId) {
        require(
            msg.sender == modules[operationId],
            ErrorCodes.ERROR_INVALID_MODULE
        );
        _;
    }

    modifier onlySelectedExecutors(uint8 operationId, address venomWallet) {
        require(
            (msg.sender == modules[operationId]) ||
            (msg.sender == _calculateUserAccountAddress(venomWallet)),
            ErrorCodes.ERROR_INVALID_EXECUTOR
        );
        _;
    }

    /**
     * @param venomWallet Address of user's venom wallet
     */
    modifier onlyValidUserAccount(address venomWallet) {
        require(
            msg.sender == _calculateUserAccountAddress(venomWallet),
            ErrorCodes.INVALID_USER_ACCOUNT
        );
        _;
    }

    modifier onlyValidUserAccountNoReserve(address venomWallet) {
        require(
            msg.sender == _calculateUserAccountAddress(venomWallet),
            ErrorCodes.INVALID_USER_ACCOUNT
        );
        _;
    }
}